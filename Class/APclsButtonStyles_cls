VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "APclsButtonStyles"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = True
Option Explicit

'===================================.
'       PUBLIC ENUM                 |
'==================================='
    Public Enum enumButtonStyles    '
        styleOn = -1                '
        styleOff = 0
        styleIdle
        styleLoad
        styleGeneric1
        styleGeneric2
        styleArmed
        styleHighContrast
        styleArmable                '
    End Enum                        '
'==================================='
    
Private Const ON_FORECOLOR = vbGreen
Private Const ON_BACKCOLOR = 32768      'glowing green
Private Const OFF_FORECOLOR = vbRed
Private Const OFF_BACKCOLOR = vbBlack
Private Const LOAD_FORECOLOR = 8438015  'soft orange
Private Const LOAD_BACKCOLOR = vbBlack  '4210752
Private Const IDLE_FORECOLOR = 12648384 'soft green
Private Const IDLE_BACKCOLOR = 4210752  'gray
Private Const GENERIC1_FORECOLOR = -2147483630
Private Const GENERIC1_BACKCOLOR = -2147483645
Private Const GENERIC2_FORECOLOR = -2147483630
Private Const GENERIC2_BACKCOLOR = -2147483646
Private Const ARMED_FORECOLOR = vbRed
Private Const ARMED_BACKCOLOR = vbYellow
Private Const HIGHCONTRAST_FORECOLOR = vbGreen
Private Const HIGHCONTRAST_BACKCOLOR = vbBlack
Private Const ARMABLE_FORECOLOR = vbYellow
Private Const ARMABLE_BACKCOLOR = 128   'burgendy

Private Enum ButtonFillColor
    onFillColor = ON_BACKCOLOR
    offFillColor = OFF_BACKCOLOR
    loadFillColor = LOAD_BACKCOLOR
    idleFillColor = IDLE_BACKCOLOR
    generic1FillColor = GENERIC1_BACKCOLOR
    generic2FillColor = GENERIC2_BACKCOLOR
    armedFillColor = ARMED_BACKCOLOR
    highContrastFillColor = HIGHCONTRAST_BACKCOLOR
    armableFillColor = ARMABLE_BACKCOLOR
End Enum

Private Enum ButtonTextColor
    onTextColor = ON_FORECOLOR
    offTextColor = OFF_FORECOLOR
    loadTextColor = LOAD_FORECOLOR
    idleTextColor = IDLE_FORECOLOR
    generic1TextColor = GENERIC1_FORECOLOR
    generic2TextColor = GENERIC2_FORECOLOR
    armedTextColor = ARMED_FORECOLOR
    highContrastTextColor = HIGHCONTRAST_FORECOLOR
    armableTextColor = ARMABLE_FORECOLOR
End Enum

Private Type typeButtonStyle
    utTextColor As ButtonTextColor
    utButtonColor As ButtonFillColor
End Type

Private mButton As MSForms.CommandButton
Private mButtonStyle As enumButtonStyles

Public Property Set AssignButton(p_Object As MSForms.CommandButton)
    Set mButton = p_Object
    Me.SetStyle = styleGeneric1
End Property

Public Property Get CurrentText() As String
    CurrentText = mButton.Caption
End Property

Public Property Let SetText(p_Text As String)
    mButton.Caption = p_Text
End Property

Public Property Let SetStyle(p_ButtonStyle As enumButtonStyles)
    mButtonStyle = p_ButtonStyle
    UpdateButtonStyle
End Property

Public Sub ToggleOnOff()
    Select Case mButtonStyle
        Case styleOn
            mButtonStyle = styleOff
        Case styleOff
            mButtonStyle = styleOn
    End Select
    UpdateButtonStyle
End Sub

Private Sub UpdateButtonStyle()
    Select Case mButtonStyle
        Case styleOn
            mButton.BackColor = ButtonFillColor.onFillColor
            mButton.ForeColor = ButtonTextColor.onTextColor
        Case styleOff
            mButton.BackColor = ButtonFillColor.offFillColor
            mButton.ForeColor = ButtonTextColor.offTextColor
        Case styleIdle
            mButton.BackColor = ButtonFillColor.idleFillColor
            mButton.ForeColor = ButtonTextColor.idleTextColor
        Case styleLoad
            mButton.BackColor = ButtonFillColor.loadFillColor
            mButton.ForeColor = ButtonTextColor.loadTextColor
        Case styleGeneric1
            mButton.BackColor = ButtonFillColor.generic1FillColor
            mButton.ForeColor = ButtonTextColor.generic1TextColor
        Case styleGeneric2
            mButton.BackColor = ButtonFillColor.generic2FillColor
            mButton.ForeColor = ButtonTextColor.generic2TextColor
        Case styleArmed
            mButton.BackColor = ButtonFillColor.armedFillColor
            mButton.ForeColor = ButtonTextColor.armedTextColor
        Case styleHighContrast
            mButton.BackColor = ButtonFillColor.highContrastFillColor
            mButton.ForeColor = ButtonTextColor.highContrastTextColor
        Case styleArmable
            mButton.BackColor = ButtonFillColor.armableFillColor
            mButton.ForeColor = ButtonTextColor.armableTextColor
    End Select
End Sub


