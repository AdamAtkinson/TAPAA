VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "clsSelectionEcho"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
'                                                                                                                                                       '
'PURPOSE:           Workaround Outlook Bug                                                                                                              '
'                                                                                                                                                       '
'BUG DESCRIPTION:   Outlook.ActiveExplorer_SelectionChange event fires twice for each event (echo)                                                      '
'                                                                                                                                                       '
'WORKAROUND:        Compare the MailItem.EntryID of the last event call to the EntryID of the current selection, it is an echo if they match            '
'                                                                                                                                                       '
'PROCEDURE:         Declare this class as a local 'Static' variable in the ActiveExplorer_SelectionChange procedure using the 'New' keywoard            '
'                   Pass the current selection's MailItem.EntryID to the class function IsSelectionChangeEcho                                           '
'                   Function returns 'True' if the last ActiveExplorer_SelectionChange used the same EntryID (is an echo)                               '
'                                                                                                                                                       '
'USE CASE:          Avoid/trap errors and avoid redundant procedural calls when using the ActiveExplrorer_SelectionChange event                         '
'                                                                                                                                                       '
'ADDITIONAL NOTES:  The current and previous EntryIDs are exposed as public readable strings which can be usefull for other purposes                    '
'                                                                                                                                                       '
'EXAMPLE:           This event procedure will run for the initial call and exit if the call is an echo                                                  '
'                                                                                                                                                       '
'                    Private Sub ActiveExplorerEvents_SelectionChange()                                                                                 '
'                        Static echoTest As clsSelectionEcho                                                                                            '
'                        If echoTest Is Nothing Then Set echoTest = New clsSelectionEcho                                                                '
'                        If echoTest.IsSelectionChangeEcho(ActiveExplorerEvents.Selection(1).EntryID) Then Exit Sub                                     '
'                        'procedure continues if an original call                                                                                       '
'                    End Sub                                                                                                                            '
'                                                                                                                                                       '

Option Explicit
Private mPreviousEntryID As String
Private mCurrentEntryID As String

Public Property Get CurrentEntryID() As String
    CurrentEntryID = mCurrentEntryID
End Property

Public Property Get PreviousEntryID() As String
    PreviousEntryID = mPreviousEntryID
End Property

Public Function IsSelectionChangeEcho(ByVal CurrentEntryID As String) As Boolean
    mCurrentEntryID = CurrentEntryID
    If CurrentEntryID = mPreviousEntryID Then IsSelectionChangeEcho = True
    mPreviousEntryID = CurrentEntryID
End Function

