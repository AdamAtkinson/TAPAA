VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "clsCustomTemplateHandler"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

Public Enum enumCONSOLE_MODES
    eASSIGNCATEGORY = 1
    eTEXTBOX = 2
End Enum


Private Const mTITLE_ASSIGNCATEGORY As String = "Toggle Selected Categories"
Private Const mCHECKBOXLABELS_ASSIGNCATEGORY As String = "Do[&H000080FF&],Delegate[&H00008080&],Defer[&H0000FFFF&],2Year[&H00FFC0C0&],Policy[&H00FF80FF&],AP Neuro[&H0080C0FF&],AP SSC[&H008080FF&],Delete[&H00FFFFFF&]"

Private WithEvents mForm As frmCustomTemplate
Attribute mForm.VB_VarHelpID = -1

Private mMode As enumCONSOLE_MODES

                                                                            '
'                                                                           |
'  CLASS INITIALIZE AND TERMINATE EVENTS                                    |
'                                                                           |
                                                                            '
Private Sub Class_Initialize()
    Set mForm = New frmCustomTemplate
End Sub

Private Sub Class_Terminate()
    If Not mForm Is Nothing Then Set mForm = Nothing
End Sub



                                                                            '
'                                                                           |
'  PRIVATE FORM EVENTS                                                      |
'                                                                           |
                                                                            '
Private Sub mForm_FormClose()
    Set mForm = Nothing
End Sub
Private Sub mForm_CancelClick()
    Select Case mMode
        Case enumCONSOLE_MODES.eASSIGNCATEGORY
            Unload mForm
            
        Case Else
            dbP "NOT BOUND"
        End Select
End Sub
Private Sub mForm_YesClick()
    Select Case mMode
        Case enumCONSOLE_MODES.eASSIGNCATEGORY
            AssignCategoriesToSelection
            
        Case Else
            dbP "NOT BOUND"
        End Select
End Sub

                                                                            '
'                                                                           |
'  PRIVATE PROCEDURES                                                       |
'                                                                           |
                                                                            '
Private Sub AssignCategoriesToSelection()
    Dim Categories As String
    Categories = mForm.SelectedCheckboxes
    Dim aryCategories() As String
    aryCategories = Split(Categories, ", ")
    Dim i As Long
    For i = 0 To UBound(aryCategories)
        AssignCategoryForSelectedItems aryCategories(i)
    Next i
End Sub

                                                                            '
'                                                                           |
'  PUBLIC LETTERS                                                           |
'                                                                           |
                                                                            '
Public Sub CreateForm(formMode As enumCONSOLE_MODES)
    If Not CBool(mMode) Then
        mMode = formMode
        Select Case mMode
            Case eASSIGNCATEGORY
                With mForm
                    .AddTitle mTITLE_ASSIGNCATEGORY
                    .AddCategoryCheckboxes
                    .AddControlButtons eYES + eCANCEL, "Toggle,Quit"
                    .Show
                End With
            Case eTEXTBOX
                With mForm
                End With
        End Select
    End If
End Sub
